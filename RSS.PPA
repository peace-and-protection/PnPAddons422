; #= pprss -rs
; ########################################
; Peace and Protection
; PnP RSS (standard addon)
; ########################################

; Manages and views RSS feeds
; Copyright (C) 2022  Sven Roelse
; 
; 
; This program is free software: you can redistribute it and/or modify
; it under the terms of the GNU General Public License as published by
; the Free Software Foundation, either version 3 of the License, or
; (at your option) any later version.
; 
; 
; This program is distributed in the hope that it will be useful,
; but WITHOUT ANY WARRANTY; without even the implied warranty of
; MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
; GNU General Public License for more details.
; 
; 
; You should have received a copy of the GNU General Public License
; along with this program.  If not, see <https://www.gnu.org/licenses/>.

#.ppa.info off
[addon]
name=RSS
group=Extras
popup=RSS
author=acvxqs
version=1.10
url=https://github.com/peace-and-protection/PnPAddons422
id=pprss
ppver=4.22
config=rss
unload=_rss.unload
vars2=rss.on rss.interval
windows=@RSS[*
dialogs=pprss
timers=.pprss.check
[files]
1=rss.ppa
[notes]
1=This addon manages and views RSS feeds.
#.ppa.info end

on *:LOAD:{
  ; PnP check
  if (!$_ispnp) {
    echo 4 -ati2 *** This addon requires Peace and Protection by pai to use.
    echo 4 -ati2 *** You can download Peace and Protection at http://www.kristshell.net/pnp/
    .timer -mio 1 0 .unload -rs " $+ $script $+ "
    halt
  }
  ; Invalid load method check
  if (!$istok($_cfgx(addons,ids),$readini($script,n,addon,id),32)) {
    .timer -mio 1 0 .unload -rs " $+ $script $+ "
    dispa Unloading ' $+ $script $+ ' $chr(40) $+ addon is not properly loaded; use /addon to load it $+ $chr(41)
    halt
  }
  `set rss.on 0
  `set rss.interval 60
  _rssw.entries
}
on *:START:{ 
  _startup.add _pprss.start Initializing RSS addon
}
alias _pprss.start {
  var %rss.on $hget(pnp.config,rss.on), %rss.interval $hget(pnp.config,rss.interval), %rss.enabledfeeds $ini($scriptdirrss.ini,enabled,0)
  if ((%rss.on) && (%rss.interval isnum 1-9999) && (%rss.enabledfeeds)) {
    .timer.pprss.check -io 0 $calc($hget(pnp.config,rss.interval) *60) _rss.checkfeeds | .timer.pprss.check -e 
  }
}
alias _rss.unload {
  if ($hget(pnp.rss.done)) hfree $v1
  var %file $scriptdirrss.ini
  if ($isfile(%file)) .remove $qt(%file)
}
alias -l _rssw.entries {
  _okcancel 1 Do you want to load default RSS feeds?
  ; De Volkskrant
  writeini -n $qt($scriptdirrss.ini) RSS https://www.volkskrant.nl/voorpagina/rss.xml $_s2f($+(Voorpagina,$chr(1),de Volkskrant))
  writeini -n $qt($scriptdirrss.ini) RSS https://www.volkskrant.nl/nieuws-achtergrond/rss.xml $_s2f($+(Nieuws & Achtergrond,$chr(1),de Volkskrant))
  writeini -n $qt($scriptdirrss.ini) RSS https://www.volkskrant.nl/columns-opinie/rss.xml $_s2f($+(Columns & Opinie,$chr(1),de Volkskrant))
  writeini -n $qt($scriptdirrss.ini) RSS https://www.volkskrant.nl/podcasts/rss.xml $_s2f($+(Podcasts,$chr(1),de Volkskrant))
  writeini -n $qt($scriptdirrss.ini) RSS https://www.volkskrant.nl/uitgelicht/rss.xml $_s2f($+(Uitgelicht,$chr(1),de Volkskrant))
  writeini -n $qt($scriptdirrss.ini) RSS https://www.volkskrant.nl/wetenschap/rss.xml $_s2f($+(Wetenschap,$chr(1),de Volkskrant))
  writeini -n $qt($scriptdirrss.ini) RSS https://www.volkskrant.nl/mensen/rss.xml $_s2f($+(Mensen,$chr(1),de Volkskrant))
  writeini -n $qt($scriptdirrss.ini) RSS https://www.volkskrant.nl/de-gids/rss.xml $_s2f($+(De Gids,$chr(1),de Volkskrant))
  writeini -n $qt($scriptdirrss.ini) RSS https://www.volkskrant.nl/reizen/rss.xml $_s2f($+(Reizen,$chr(1),de Volkskrant))
  writeini -n $qt($scriptdirrss.ini) RSS https://www.volkskrant.nl/opvoeden/rss.xml $_s2f($+(Opvoeden,$chr(1),de Volkskrant))
  writeini -n $qt($scriptdirrss.ini) RSS https://www.volkskrant.nl/eten-en-drinken/rss.xml $_s2f($+(Eten en Drinken,$chr(1),de Volkskrant))
  writeini -n $qt($scriptdirrss.ini) RSS https://www.volkskrant.nl/kijken/rss.xml $_s2f($+(Kijken,$chr(1),de Volkskrant))
  writeini -n $qt($scriptdirrss.ini) RSS https://www.volkskrant.nl/werken/rss.xml $_s2f($+(Werken,$chr(1),de Volkskrant))
  writeini -n $qt($scriptdirrss.ini) RSS https://www.volkskrant.nl/cultuur-media/rss.xml $_s2f($+(Cultuur & Media,$chr(1),de Volkskrant))
  writeini -n $qt($scriptdirrss.ini) RSS https://www.volkskrant.nl/foto/rss.xml $_s2f($+(Foto,$chr(1),de Volkskrant))
  writeini -n $qt($scriptdirrss.ini) RSS https://www.volkskrant.nl/economie/rss.xml $_s2f($+(Economie,$chr(1),de Volkskrant))
  writeini -n $qt($scriptdirrss.ini) RSS https://www.volkskrant.nl/sport/rss.xml $_s2f($+(Sport,$chr(1),de Volkskrant)) 
  ; The New York Times
  writeini -n $qt($scriptdirrss.ini) RSS https://rss.nytimes.com/services/xml/rss/nyt/HomePage.xml $_s2f($+(Home Page (U.S.),$chr(1),NYTimes))
  writeini -n $qt($scriptdirrss.ini) RSS https://rss.nytimes.com/services/xml/rss/nyt/World.xml $_s2f($+(World,$chr(1),NYTimes)) 
  writeini -n $qt($scriptdirrss.ini) RSS https://rss.nytimes.com/services/xml/rss/nyt/Africa.xml $_s2f($+(Africa,$chr(1),NYTimes))
  writeini -n $qt($scriptdirrss.ini) RSS https://rss.nytimes.com/services/xml/rss/nyt/Americas.xml $_s2f($+(Americas,$chr(1),NYTimes))
  writeini -n $qt($scriptdirrss.ini) RSS https://rss.nytimes.com/services/xml/rss/nyt/AsiaPacific.xml $_s2f($+(Asia Pacific,$chr(1),NYTimes))
  writeini -n $qt($scriptdirrss.ini) RSS https://rss.nytimes.com/services/xml/rss/nyt/Europe.xml $_s2f($+(Europe,$chr(1),NYTimes))
  writeini -n $qt($scriptdirrss.ini) RSS https://rss.nytimes.com/services/xml/rss/nyt/MiddleEast.xml $_s2f($+(Middle East,$chr(1),NYTimes))
  writeini -n $qt($scriptdirrss.ini) RSS https://rss.nytimes.com/services/xml/rss/nyt/Education.xml $_s2f($+(Education,$chr(1),NYTimes))
  writeini -n $qt($scriptdirrss.ini) RSS https://rss.nytimes.com/services/xml/rss/nyt/Politics.xml $_s2f($+(Politics,$chr(1),NYTimes))
  writeini -n $qt($scriptdirrss.ini) RSS https://rss.nytimes.com/services/xml/rss/nyt/Economy.xml $_s2f($+(Economy,$chr(1),NYTimes))
  writeini -n $qt($scriptdirrss.ini) RSS https://rss.nytimes.com/services/xml/rss/nyt/Technology.xml $_s2f($+(Technology,$chr(1),NYTimes))
  writeini -n $qt($scriptdirrss.ini) RSS https://rss.nytimes.com/services/xml/rss/nyt/Sports.xml $_s2f($+(Sports,$chr(1),NYTimes))
  writeini -n $qt($scriptdirrss.ini) RSS https://rss.nytimes.com/services/xml/rss/nyt/Science.xml $_s2f($+(Science,$chr(1),NYTimes))
  writeini -n $qt($scriptdirrss.ini) RSS https://rss.nytimes.com/services/xml/rss/nyt/Climate.xml $_s2f($+(Climate,$chr(1),NYTimes))
  writeini -n $qt($scriptdirrss.ini) RSS https://www.nytimes.com/services/xml/rss/nyt/Health.xml $_s2f($+(Health,$chr(1),NYTimes))
  writeini -n $qt($scriptdirrss.ini) RSS https://rss.nytimes.com/services/xml/rss/nyt/ArtandDesign.xml $_s2f($+(Art & Design,$chr(1),NYTimes))
  writeini -n $qt($scriptdirrss.ini) RSS https://rss.nytimes.com/services/xml/rss/nyt/Music.xml $_s2f($+(Music,$chr(1),NYTimes))
  writeini -n $qt($scriptdirrss.ini) RSS https://rss.nytimes.com/services/xml/rss/nyt/Movies.xml $_s2f($+(Movies,$chr(1),NYTimes))
  writeini -n $qt($scriptdirrss.ini) RSS https://rss.nytimes.com/services/xml/rss/nyt/Television.xml $_s2f($+(Television,$chr(1),NYTimes))
  ; Ars Technica
  writeini -n $qt($scriptdirrss.ini) RSS https://arstechnica.com/feed/ $_s2f($+(Main Page,$chr(1),Ars Technica)) 
  ; XKCD.com
  writeini -n $qt($scriptdirrss.ini) RSS https://xkcd.com/rss.xml $_s2f($+(Main Page,$chr(1),XKCD)) 
}
alias rss {
  if (($1 == on) || ($1 == on)) {
    if ($ini($scriptdirrss.ini,enabled,0)) {
      `set rss.on 1 
    }
    else { 
      dispa No RSS feeds enabled!
      `set rss.on 0
    }
  }
  elseif (($1 == off) || ($1 == off)) {
    `set rss.on 0
  }
  else {
    _dialog -am pprss pprss
    return
  }
  if ($hget(pnp.config,rss.on)) {
    .timer.pprss.check -io 0 $calc($hget(pnp.config,rss.interval) *60) _rss.checkfeeds | .timer.pprss.check -e
  }
  else {
    if ($hget(pnp.rss.done)) hfree pnp.rss.done
    .timer.pprss.check off
    close -@RSS[*
  }
  dispa RSS is $:t($_tf2o($hget(pnp.config,rss.on)))
}
alias rsspull if ($timer(.pprss.check)) .timer.pprss.check -e
alias -l _rss.checkfeeds {
  var %i 1
  while ($ini($scriptdirrss.ini,Enabled,%i)) {
    var %url $v1, %data $readini($scriptdirrss.ini,n,Enabled,%url)
    tokenize 1 %data
    var %win @RSS[ $+ $2 $+ - $+ $1 $+ ]
    if (!$window(%win)) _window 2 -niz + %win -1 -1 -1 -1 @RSS
    _rssget %url %win 
    inc %i
  }
}
; IXMLDOMElement Members
; https://docs.microsoft.com/en-us/previous-versions/windows/desktop/ms757048(v=vs.85)
alias _rssget {
  var %error, %pattern https?:\/\/(?:www\.)?([-a-zA-Z0-9@:%._\+~#=]{1,256}\.[a-zA-Z0-9()]{1,6})\b([-a-zA-Z0-9()@:%_\+.~#?&\/\/=]*)
  if (!$regex($1,%pattern)) %error = Invalid URL!
  else {
    var %t $ticks, %itemcol itemcol. $+ %t, %com rss. $+ %t, %com2 rss2. $+ %t, %com3 rss3. $+ %t, %url $1
    .comopen %com MSXML2.DOMDocument.6.0
    if ((!$com(%com)) || ($comerr)) %error = Unable to create an instance of MSXML2.DOMDocument.6.0
    elseif ((!$com(%com, async, 4, bool, false)) || ($comerr)) %error = Unable to set async property
    elseif ((!$com(%com, load, 1, bstr*, %url)) || ($comerr)) %error = Unable to load XML from: ' $+ %url $+ '
    elseif ((!$com(%com, selectnodes, 1, bstr*, /rss/channel/item, dispatch* %itemcol)) || ($comerr)) %error = Unable to select nodes /rss/channel/item
    else {
      var %item 0, %items $comval(%itemcol,0)
      if (%items) {
        while ($com(%itemcol, item, 1, uint, %item, dispatch* %com2)) {
          if ($com(%com2)) {

            if ((!$com(%com2, xml, 3)) || ($comerr)) %error = Cannot get xml from individual item
            if ((!$com(%com2,&xml).result) || ($comerr)) %error = Cannot place xml in binvar
            else {
              var %sha1 $sha1(&xml,1)
              bunset &xml
              if ((!$com(%com2, childnodes, 3, dispatch* %com3)) || ($comerr)) %error = Cannot get childnodes from individual /item

              .comclose %com2
              if (!$hget(pnp.rss.done,%sha1)) {
                hadd -m pnp.rss.done %sha1 1
                if ($com(%com3)) {
                  var %children $comval(%com3,0), %child 1
                  if (%children) {
                    if ($window($2)) window -g1 $2
                    while (%child <= %children) {
                      var %tagname $comval(%com3,%child,tagName)
                      if (%child == 1) $iif($window($2),aline $2,echo -sg) $timestamp $:::
                      if ($istok(title,%tagname,32)) $iif($window($2),aline -p $2,echo -sg) $timestamp $:b($_cap1st(%tagname)) : $iif($hget(pnp.config,rss.boldtitle),$:b($comval(%com3,%child,text)),$comval(%com3,%child,text))
                      if ($istok(link description pubDate,%tagname,32)) $iif($window($2),aline -p $2,echo -sg) $timestamp $:b($_cap1st(%tagname)) : $comval(%com3,%child,text)
                      inc %child
                    }
                  }
                  else %error = No child nodes found for individual item!
                }
              }
              .comclose %com3
            }
          }
          else break
          inc %item
        }
      }
      else %error = No items found for url ' $+ %url $+ '
    }
  }
  ; Handle errors
  :error
  if ($error) var %error $v1 | reseterror

  ; Close coms
  if ($com(%com)) .comclose %com
  if ($com(%com2)) .comclose %com2
  if ($com(%com3)) .comclose %com3
  if ($com(%itemcol)) .comclose %itemcol

  ; Show error
  if (%error) dispa $:w($v1)
}
alias -l _rss.shorten {
  if ($len($2-) > $1) return $left($2-,$calc($1 -3)) $+ ...
  else return $2-
}
dialog pprss {
  title "RSS Central"
  size -1 -1 292 182
  ;size -1 -1 600 182
  option dbu
  icon script\pnp.ico

  check "Enable RSS", 1, 5 5 50 10
  text "Poll xml feeds every", 2, 5 20 48 8
  edit "", 3, 56 19 20 10
  text "minutes. (10 or more recommended)", 4, 79 20 90 8
  check "Bold 'Title' text in RSS window", 9, 200 20 90 8
  list 5, 5 50 280 100, size check
  button "Add", 6, 5 155 37 12
  button "Edit", 7, 47 155 37 12, disable
  button "Remove", 8, 89 155 37 12, disable
  combo 10, 5 35 280 70, size drop

  button "Cancel", 11, 206 155 37 12,cancel 
  button "OK", 12, 248 155 37 12, ok default
  list 99, 305 50 280 100, hide
}
alias -l _rssd.ok {
  `set rss.interval $iif(($did(3) isnum 1-9999),$did(3),60)
  `set rss.on $did(1).state
}
on *:DIALOG:pprss:init:0:{
  did $iif($hget(pnp.config,rss.on),-c,-u) $dname 1
  did $iif($hget(pnp.config,rss.boldtitle),-c,-u) $dname 9
  did -a $dname 3 $hget(pnp.config,rss.interval)
  var %i 1
  while ($ini($scriptdirrss.ini,Enabled,%i)) {
    var %url $v1, %data $readini($scriptdirrss.ini,n,Enabled,%url), %name $gettok(%data,1,1), %group $gettok(%data,2,1)
    var %string $+(%name,$chr(1),-,$chr(1),%url,$chr(1),%group,$chr(32),1)
    did -a $dname 99 %string
    inc %i
  }
  _rssd.updcat
}
on *:DIALOG:pprss:sclick:5:{
  did -e $dname 7,8
  _rssd.rssstate
}
on *:DIALOG:pprss:dclick:5:{
  did -e $dname 7,8
  _rssd.rssstate
}
on *:DIALOG:pprss:sclick:6:{
  %.fullmsg = Select group name:
  %.found = $addtok($didtok(10,44),(Make a new group),44)
  var %group $$dialog(nickcomp,nickcomp,-4)
  if (%group == (Make a new group)) %group = $_s2f($_entry(0,$null,Please enter the name of the group:))
  var %name $_s2f($_entry(0,$null,Please enter the name of the feed:))
  var %url $_entry(-1,$null,Please enter the url of the feed:)
  writeini -n $qt($scriptdirrss.ini) RSS %url $+(%name,$chr(1),%group)
  _rssd.updcat
  _rssd.updrss
}
on *:DIALOG:pprss:sclick:7:{ 
  var %group $did(10)
  var %string $replace($+($did(5,$did(5).sel),$chr(1),%group),$chr(32),$chr(1))
  var %newname $_s2f($_entry(0,$gettok($did(5,$did(5).sel),1,32),Please enter the new name of the feed:))
  var %newfeed $_entry(-1,$gettok($did(5,$did(5).sel),-1,32),Please enter the new url of the feed:)
  var %newstring $replace($+(%newname - %newfeed,$chr(1),$did(10)),$chr(32),$chr(1))
  if (%newstring == %string) _error Old data and new data are the same!
  var %finddid $_finddid($dname,99,%string)
  if (%finddid) did -o $dname 99 %finddid %newstring $did(5,$did(5).sel).cstate
  else did -a $dname 99 %newstring $did(5,$did(5).sel).cstate
  remini $qt($scriptdirrss.ini) RSS $gettok($did(5,$did(5).sel),3,32)
  writeini -n $qt($scriptdirrss.ini) RSS %newfeed $+(%newname,$chr(1),%group)
  _rssd.updrss
}
on *:DIALOG:pprss:sclick:9:{
  `set rss.boldtitle $did(9).state
}
on *:DIALOG:pprss:sclick:12:{ 
  var %rss.interval.old $hget(pnp.config,rss.interval)
  var %rss.interval.new $iif($did(3) isnum 1-9999,$did(3),60)
  var %rss.on.old $hget(pnp.config,rss.on)
  var %rss.on.new $did(1).state

  var %i 1, %lines $did(99).lines
  while ($did(99,%i)) {
    var %string $v1, %data $gettok(%string,1,32), %state $gettok(%string,2,32)
    var %url $gettok(%data,3,1), %name $gettok(%data,1,1), %group $gettok(%data,4,1)
    var %enabled $readini($scriptdirrss.ini,n,Enabled,%url)

    if ((%enabled) && (%state == R)) { 
      remini $qt($scriptdirrss.ini) Enabled %url
      remini $qt($scriptdirrss.ini) RSS %url
    }
    elseif ((%enabled) && (%state == 0)) {
      remini $qt($scriptdirrss.ini) Enabled %url
    }
    elseif ((!%enabled) && (%state == R)) {
      remini $qt($scriptdirrss.ini) RSS %url
    }
    elseif ((!%enabled) && (%state == 1)) {
      var %data $+(%name,$chr(1),%group)
      writeini -n $qt($scriptdirrss.ini) Enabled %url %data
      if (%rss.on.new) {
        tokenize 1 %data
        var %win @RSS[ $+ $2 $+ - $+ $1 $+ ]
        if (!$window(%win)) _window 2 -niz + %win -1 -1 -1 -1 @RSS
        _rssget %url %win
      }
    }
    inc %i
  }
  ; turn off if no enabled feeds found:
  if (!$ini($scriptdirrss.ini,enabled,0)) %rss.on.new = 0

  ; turned on
  if (%rss.on.new > %rss.on.old) {

    .timer.pprss.check -io 0 $calc(%rss.interval.new *60) _rss.checkfeeds | timer.pprss.check -e
  }
  ; turned off
  elseif (%rss.on.new < %rss.on.old) {
    if ($hget(pnp.rss.done)) hfree pnp.rss.done
    .timer.pprss.check off
    close -@RSS[*
  }
  ; no change in RSS enabled state
  else {
    ; interval has changed
    if (%rss.interval.new !== %rss.interval.old) {
      if ($timer(.pprss.check)) {
        var %new_interval_in_secs $calc(%rss.interval.new *60)
        var %old_interval_in_secs $calc(%rss.interval.old *60)
        var %difference_between_old_value_and_time_left $calc(%old_interval_in_secs - $timer(.pprss.check).secs)
        if (%difference_between_old_value_and_time_left >= %new_interval_in_secs) {
          .timer.pprss.check -io 0 %new_interval_in_secs _rss.checkfeeds | .timer.pprss.check -e
        }
        else { 
          .timer.pprss.check -io 0 %new_interval_in_secs _rss.checkfeeds
        }
      }
    }
  }  
  `set rss.interval %rss.interval.new
  `set rss.on %rss.on.new
}
alias -l _rssd.rssstate {
  var %rssstate $did(5,$did(5).sel).cstate, %string $replace($+($did(5,$did(5).sel),$chr(1),$did(10)),$chr(32),$chr(1))
  var %finddid $_finddid($dname,99,%string)
  goto %rssstate $+ $iif(%finddid,1,0)

  :00
  return

  :01
  did -o $dname 99 %finddid $+(%string,$chr(32),0) 
  return

  :10
  did -a $dname 99 $+(%string,$chr(32),1)
  return

  :11
  did -o $dname 99 %finddid $+(%string,$chr(32),1)
  return
}
on *:DIALOG:pprss:sclick:8:{
  _okcancel 1 Remove ' $+ $did(5,$did(5).sel) $+ '?
  var %string $replace($+($did(5,$did(5).sel),$chr(1),$did(10)),$chr(32),$chr(1))
  var %finddid $_finddid($dname,99,%string)
  if (%finddid) did -o $dname 99 %finddid $+(%string,$chr(32),R)
  else did -a $dname 99 $+(%string,$chr(32),R)
  _rssd.updrss
}
on *:DIALOG:pprss:sclick:10:_rssd.updrss
alias -l _rssd.updcat {
  var %i 1
  while ($ini($scriptdirrss.ini,RSS,%i)) {
    var %url $v1, %data $readini($scriptdirrss.ini,n,RSS,%url)
    tokenize 1 %data
    if (!$_scandid($dname,10,$2)) did -a $dname 10 $2
    inc %i
  }
}
alias -l _rssd.updrss {
  did -r $dname 5
  did -b $dname 7,8
  var %i 1
  while ($ini($scriptdirrss.ini,RSS,%i)) {
    var %url $v1, %data $readini($scriptdirrss.ini,n,RSS,%url)
    tokenize 1 %data
    if ($did(10) == $2) {
      var %string $1 - %url, %entry $replace($+(%string,$chr(1),$2),$chr(32),$chr(1))
      if ($_finddid($dname,99,%entry)) {
        var %tok $gettok($did($dname,99,$v1),2,32)

        if (%tok == 0) {
          did -a $dname 5 %string
        }
        elseif (%tok == 1) {
          did -a $dname 5 %string
          did -s $dname 5 $did(5).lines
        }
      }
      else {
        did -a $dname 5 %string 
        did -l $dname 5 $did(5).lines
      }
    }
    inc %i
  }
}
menu @RSS {
  Configure...:rss
  -
  Pull feed(s) now!:rsspull
}
